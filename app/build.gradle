apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 9
def versionPatch = 4
def versionBuild = 0

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.honu.giftwise"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            keyAlias 'honu'
            storeFile file('../../android/keys/honu-apps.keystore')
            storePassword System.getenv("KEYSTORE_PWD")
            keyPassword System.getenv("KEY_PWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def fileName = applciationName + "-" + variant.buildType.name + "-v" + defaultConfig.versionName + ".apk";
            output.outputFile = new File(output.outputFile.parent, fileName)
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'honuapps:aloha:0.9.+'
    compile project(':colorpicker')
}

task copyAppConfig(type: Copy) {
    String appDir = System.getenv('HOME') + '/.android/dev/' + android.defaultConfig.applicationId
    println "Copying application configuration settings from: " + appDir
    from appDir
    into 'src/main/res/values'
}

clean.dependsOn copyAppConfig
clean.mustRunAfter copyAppConfig